import QtQuick
import QtQuick.Controls.Basic
import Quickshell
import Quickshell.Hyprland._FocusGrab
import qs
import "../lucidnotif"

// the bar's entry point into the notification centre. the state lives in the
// Notifs singleton; popups are their own surface now, so this is the shade.
BarPill {
    id: root

    readonly property bool silenced: Notifs.silenced
    readonly property int notifCount: Notifs.count
    readonly property string badgeDisplayText: root.notifCount > 9 ? "9+" : String(root.notifCount)
    readonly property bool anyCritical: Notifs.criticalCount > 0
    readonly property color badgeColor: root.anyCritical ? Theme.error : Theme.accent
    readonly property color badgeTextColor: root.anyCritical ? Theme.fgError : Theme.fgAccent
    // the face's looks, from its card on the Bar page: the count on a badge,
    // a dot instead, or the bell and the count together on a chip
    readonly property bool dotFace: Prefs.notificationsStyle === "dot"
    readonly property bool chipFace: Prefs.notificationsStyle === "chip" && root.notifCount > 0
    readonly property bool badgeShown: root.notifCount > 0 && !root.dotFace
    readonly property int horizontalPadding: 10
    readonly property real screenW: root.hostWindow ? root.hostWindow.screen.width : 1600
    readonly property real screenH: root.hostWindow ? root.hostWindow.screen.height : 900
    readonly property int maxPanelHeight: Math.min(560, Math.max(220, root.screenH - 80))
    // measured, not guessed, so the list always gets the rest
    readonly property int chromeHeight: headerRow.height + countRow.height + footerRow.height + mainColumn.spacing * 3 + 28

    readonly property var popups: Notifs.popups
    readonly property var topPopup: root.popups.length > 0 ? root.popups[0] : null
    readonly property var restPopups: root.popups.length > 1 ? root.popups.slice(1) : []
    readonly property int popupOverflow: Math.max(0, Notifs.count - root.popups.length)

    shown: Prefs.showNotifications && !(Prefs.notificationsHideEmpty && root.notifCount === 0 && !root.silenced)
    compactWidth: compactRow.implicitWidth + root.horizontalPadding * 2
    panelWidth: Math.min(380, root.screenW - 34)
    panelHeight: Math.min(root.maxPanelHeight, mainColumn.implicitHeight + 28)
    expandedRadius: Theme.shapeXl
    // off on the other bars when there is one on every display
    property bool showsPopups: true

    // the module becomes the newest popup; the older ones stack under it
    altOpen: root.showsPopups && root.shown && root.topPopup !== null && !root.expanded
    altWidth: Math.min(344, root.screenW - 34)
    altHeight: Math.min(root.maxPanelHeight, topCard.implicitHeight)
    overlayOpen: root.altOpen && (root.restPopups.length > 0 || root.popupOverflow > 0)
    overlayItem: stackSheet

    property var shownPopup: null

    onTopPopupChanged: {
        if (!root.topPopup) {
            // keep the last one on screen while the pill collapses over it
            clearShownTimer.restart();
            return ;
        }
        clearShownTimer.stop();
        root.beginTransition();
        if (!root.shownPopup) {
            root.shownPopup = root.topPopup;
            return ;
        }
        if (root.shownPopup !== root.topPopup)
            popupSwap.restart();

    }
    onExpandedChanged: {
        Notifs.shadeOpen = root.expanded;
        if (root.expanded)
            shadeList.contentY = 0;

    }

    // the popup lives on a layer with no keyboard interactivity, so a reply
    // field in it needs its own grab the way the expanded panel gets one
    HyprlandFocusGrab {
        active: Notifs.replying && !root.expanded
        windows: root.hostWindow ? [root.hostWindow] : []
        onCleared: Notifs.replyingId = -1
    }

    Timer {
        id: clearShownTimer

        interval: Theme.barMs(420)
        onTriggered: root.shownPopup = null
    }

    // out, swap, in — so an expiring popup is seen to leave
    SequentialAnimation {
        id: popupSwap

        ParallelAnimation {
            NumberAnimation {
                target: topCard
                property: "opacity"
                to: 0
                duration: Theme.barMs(110)
                easing.type: Easing.InCubic
            }

            NumberAnimation {
                target: topCard
                property: "swapOffset"
                to: -16
                duration: Theme.barMs(110)
                easing.type: Easing.InCubic
            }

        }

        ScriptAction {
            script: {
                root.shownPopup = root.topPopup;
                topCard.swapOffset = 14;
                root.beginTransition();
            }
        }

        ParallelAnimation {
            NumberAnimation {
                target: topCard
                property: "opacity"
                to: 1
                duration: Theme.barMs(210)
                easing.type: Easing.OutCubic
            }

            NumberAnimation {
                target: topCard
                property: "swapOffset"
                to: 0
                duration: Theme.barMs(210)
                easing.type: Easing.OutCubic
            }

        }

    }

    Connections {
        function onShadeRequested() {
            root.expanded = true;
        }

        function onShadeCloseRequested() {
            root.expanded = false;
        }

        function onShadeToggleRequested() {
            root.expanded = !root.expanded;
        }

        target: Notifs
    }

    compactContent: [
        Rectangle {
            anchors.centerIn: compactRow
            width: compactRow.width + 14
            height: Math.min(24, parent.height - 6)
            radius: Theme.pill(height)
            color: root.badgeColor
            opacity: root.chipFace ? 1 : 0
            visible: opacity > 0.01

            Behavior on opacity {
                NumberAnimation {
                    duration: Theme.barMs(220)
                    easing.type: Easing.OutCubic
                }

            }

        },
        Row {
            id: compactRow

            anchors.centerIn: parent
            spacing: 5

            Item {
                width: 17
                height: 17
                anchors.verticalCenter: parent.verticalCenter

                NotifIcon {
                    anchors.centerIn: parent
                    size: 17
                    path: Notifs.icons.notifications
                    color: root.chipFace ? root.badgeTextColor : Theme.text
                    opacity: root.silenced ? 0 : 1
                    scale: root.silenced ? 0.55 : 1
                    rotation: root.silenced ? -30 : 0
                    visible: opacity > 0.01

                    Behavior on opacity {
                        NumberAnimation {
                            duration: Theme.barMs(220)
                            easing.type: Easing.OutCubic
                        }

                    }

                    Behavior on scale {
                        NumberAnimation {
                            duration: Theme.barMs(260)
                            easing.type: Easing.OutBack
                        }

                    }

                    Behavior on rotation {
                        NumberAnimation {
                            duration: Theme.barMs(220)
                            easing.type: Easing.OutCubic
                        }

                    }

                }

                NotifIcon {
                    anchors.centerIn: parent
                    size: 17
                    path: Notifs.icons.bedtime
                    color: Theme.accent
                    opacity: root.silenced ? 1 : 0
                    scale: root.silenced ? 1 : 0.55
                    rotation: root.silenced ? 0 : 30
                    visible: opacity > 0.01

                    Behavior on opacity {
                        NumberAnimation {
                            duration: Theme.barMs(220)
                            easing.type: Easing.OutCubic
                        }

                    }

                    Behavior on scale {
                        NumberAnimation {
                            duration: Theme.barMs(260)
                            easing.type: Easing.OutBack
                        }

                    }

                    Behavior on rotation {
                        NumberAnimation {
                            duration: Theme.barMs(220)
                            easing.type: Easing.OutCubic
                        }

                    }

                }

                // the dot face: something is waiting, without the count
                Rectangle {
                    anchors.right: parent.right
                    anchors.top: parent.top
                    anchors.rightMargin: -1
                    width: 7
                    height: 7
                    radius: 3.5
                    color: root.badgeColor
                    border.width: 1.5
                    border.color: Theme.bg
                    opacity: root.dotFace && root.notifCount > 0 && !root.silenced ? 1 : 0
                    scale: opacity > 0 ? 1 : 0.4
                    visible: opacity > 0.01

                    Behavior on opacity {
                        NumberAnimation {
                            duration: Theme.barMs(220)
                            easing.type: Easing.OutCubic
                        }

                    }

                }

            }

            Rectangle {
                id: badge

                anchors.verticalCenter: parent.verticalCenter
                height: 16
                width: root.badgeShown ? Math.max(16, badgeText.implicitWidth + 8) : 0
                radius: Theme.pill(height)
                color: root.chipFace ? "transparent" : root.badgeColor
                opacity: root.badgeShown ? 1 : 0
                scale: root.badgeShown ? 1 : 0.4
                clip: true

                Text {
                    id: badgeText

                    property string displayedText: ""
                    property real morphOffset: 0

                    anchors.centerIn: parent
                    anchors.verticalCenterOffset: badgeText.morphOffset
                    text: badgeText.displayedText
                    color: root.badgeTextColor
                    font.family: Theme.fontFamily
                    font.bold: true
                    font.pixelSize: Theme.fs(11)
                    Component.onCompleted: badgeText.displayedText = root.badgeDisplayText
                }

                Connections {
                    function onBadgeDisplayTextChanged() {
                        badgeMorph.restart();
                    }

                    target: root
                }

                // the count rolls over rather than snapping
                SequentialAnimation {
                    id: badgeMorph

                    ParallelAnimation {
                        NumberAnimation {
                            target: badgeText
                            property: "opacity"
                            to: 0
                            duration: Theme.barMs(90)
                            easing.type: Easing.InCubic
                        }

                        NumberAnimation {
                            target: badgeText
                            property: "morphOffset"
                            to: -6
                            duration: Theme.barMs(90)
                            easing.type: Easing.InCubic
                        }

                    }

                    ScriptAction {
                        script: {
                            badgeText.displayedText = root.badgeDisplayText;
                            badgeText.morphOffset = 6;
                        }
                    }

                    ParallelAnimation {
                        NumberAnimation {
                            target: badgeText
                            property: "opacity"
                            to: 1
                            duration: Theme.barMs(140)
                            easing.type: Easing.OutCubic
                        }

                        NumberAnimation {
                            target: badgeText
                            property: "morphOffset"
                            to: 0
                            duration: Theme.barMs(140)
                            easing.type: Easing.OutCubic
                        }

                    }

                }

                // no bounce here, it fights the pop below
                Behavior on width {
                    NumberAnimation {
                        duration: Theme.barMs(160)
                        easing.type: Easing.OutCubic
                    }

                }

                Behavior on color {
                    ColorAnimation {
                        duration: Theme.barMs(220)
                    }

                }

                Behavior on opacity {
                    NumberAnimation {
                        duration: Theme.barMs(180)
                    }

                }

                Behavior on scale {
                    NumberAnimation {
                        duration: Theme.barMs(220)
                        easing.type: Easing.OutBack
                    }

                }

            }

        },
        // left click opens the shade through BarPill; this only wants the right
        MouseArea {
            anchors.fill: parent
            acceptedButtons: Qt.RightButton
            onClicked: Notifs.toggleDnd()
        }
    ]

    altContent: [
        NotifPopupCard {
            id: topCard

            anchors.left: parent.left
            anchors.right: parent.right
            anchors.top: parent.top
            notification: root.shownPopup
        }
    ]

    overlayContent: [
        Item {
            id: stackSheet

            // parked directly under whichever surface the pill is wearing
            // (over it, newest nearest the bar, on a bottom bar)
            readonly property real liveY: root.popupItem ? root.overlayEdgeY(root.surfaceReach + 8, stackSheet.height) : 0
            property real heldY: 0

            onLiveYChanged: {
                if (root.overlayOpen)
                    stackSheet.heldY = stackSheet.liveY;

            }
            // the pill's outer edge (the one on the side its group sits against)
            // is its fixed point: anchoring there keeps the sheet still while
            // the pill collapses and slides underneath it
            x: root.popupAlign === "left" ? 0 : (root.popupAlign === "center" ? (root.width - stackSheet.width) / 2 : root.width - stackSheet.width)
            // frozen while fading, or it flies up after the shrinking pill
            y: root.overlayOpen ? stackSheet.liveY : stackSheet.heldY
            width: root.altWidth
            height: stackColumn.implicitHeight
            opacity: root.overlayOpen ? 1 : 0
            visible: opacity > 0.01

            HoverHandler {
                onHoveredChanged: Notifs.popupsPaused = hovered
            }

            // the list, then the overflow chip, going away from the bar
            Item {
                id: stackColumn

                readonly property bool up: root.atBottom
                readonly property real overflowSpace: overflowItem.visible ? overflowItem.height + 8 : 0

                width: parent.width
                implicitHeight: stackList.height + stackColumn.overflowSpace

                ListView {
                    id: stackList

                    y: stackColumn.up ? stackColumn.overflowSpace : 0
                    width: parent.width
                    height: contentHeight
                    interactive: false
                    spacing: 8
                    verticalLayoutDirection: stackColumn.up ? ListView.BottomToTop : ListView.TopToBottom
                    model: stackModel

                    Behavior on height {
                        NumberAnimation {
                            duration: Theme.barMs(260)
                            easing.type: Easing.Bezier
                            easing.bezierCurve: Theme.easeEmphasizedDecel
                        }

                    }

                    delegate: NotifPopupCard {
                        required property var modelData

                        width: stackList.width
                        notification: modelData
                        surfaceColor: Theme.withBlur(Theme.bg)
                    }

                    add: Transition {
                        NumberAnimation {
                            property: "opacity"
                            from: 0
                            to: 1
                            duration: Theme.barMs(260)
                            easing.type: Easing.OutCubic
                        }

                    }

                    remove: Transition {
                        NumberAnimation {
                            property: "opacity"
                            to: 0
                            duration: Theme.barMs(170)
                            easing.type: Easing.InCubic
                        }

                        NumberAnimation {
                            property: "scale"
                            to: 0.92
                            duration: Theme.barMs(170)
                            easing.type: Easing.InCubic
                        }

                    }

                    displaced: Transition {
                        NumberAnimation {
                            properties: "x,y"
                            duration: Theme.barMs(300)
                            easing.type: Easing.Bezier
                            easing.bezierCurve: Theme.easeEmphasizedDecel
                        }

                        NumberAnimation {
                            property: "opacity"
                            to: 1
                            duration: Theme.barMs(200)
                        }

                    }

                }

                // whatever the stack could not hold is still in the shade
                Item {
                    id: overflowItem

                    y: stackColumn.up ? 0 : stackList.height + 8
                    width: parent.width
                    height: root.popupOverflow > 0 ? 26 : 0
                    opacity: root.popupOverflow > 0 ? 1 : 0
                    visible: opacity > 0.01

                    Rectangle {
                        anchors.centerIn: parent
                        width: overflowRow.implicitWidth + 22
                        height: 26
                        radius: Theme.pill(height)
                        color: overflowArea.containsMouse ? Theme.withBlur(Theme.bgActive) : Theme.withBlur(Theme.bg)

                        Row {
                            id: overflowRow

                            anchors.centerIn: parent
                            spacing: 4

                            Text {
                                anchors.verticalCenter: parent.verticalCenter
                                text: I18n.tr("%1 more", root.popupOverflow)
                                color: Theme.subtext
                                font.family: Theme.fontFamily
                                font.bold: true
                                font.pixelSize: Theme.fs(10)
                            }

                            NotifIcon {
                                anchors.verticalCenter: parent.verticalCenter
                                size: 13
                                path: Notifs.icons.expand_more
                                // towards the shade, which opens off the bar
                                rotation: root.atBottom ? 180 : 0
                                color: Theme.subtext
                            }

                        }

                        MouseArea {
                            id: overflowArea

                            anchors.fill: parent
                            hoverEnabled: true
                            cursorShape: Qt.PointingHandCursor
                            onClicked: {
                                Notifs.popupClear();
                                root.expanded = true;
                            }
                        }

                        Behavior on color {
                            ColorAnimation {
                                duration: Theme.barMs(120)
                            }

                        }

                    }

                    Behavior on height {
                        NumberAnimation {
                            duration: Theme.barMs(220)
                            easing.type: Easing.OutCubic
                        }

                    }

                }

            }

            Behavior on opacity {
                NumberAnimation {
                    duration: Theme.barMs(200)
                }

            }

        }
    ]

    panelContent: [
        Column {
            id: mainColumn

            anchors.fill: parent
            anchors.margins: 14
            spacing: 8

            Item {
                id: headerRow

                width: parent.width
                height: 30

                Text {
                    anchors.left: parent.left
                    anchors.verticalCenter: parent.verticalCenter
                    text: I18n.tr("Notifications")
                    color: Theme.text
                    font.family: Theme.fontFamily
                    font.weight: Font.DemiBold
                    font.pixelSize: Theme.fs(15)
                }

                Row {
                    anchors.right: parent.right
                    anchors.verticalCenter: parent.verticalCenter
                    spacing: 2

                    NotifGhostButton {
                        iconPath: Notifs.icons.bedtime
                        active: Notifs.dnd
                        onClicked: Notifs.toggleDnd()
                    }

                    NotifGhostButton {
                        iconPath: Notifs.icons.settings
                        onClicked: {
                            root.expanded = false;
                            Notifs.settingsRequested();
                        }
                    }

                }

            }

            // what is actually going on right now, in one line
            Item {
                id: countRow

                width: parent.width
                height: 22

                Text {
                    anchors.left: parent.left
                    anchors.verticalCenter: parent.verticalCenter
                    text: {
                        if (Notifs.dnd)
                            return I18n.tr("Do Not Disturb · %1 waiting", root.notifCount);

                        if (Notifs.quietNow)
                            return I18n.tr("Quiet hours · %1 waiting", root.notifCount);

                        if (root.notifCount === 0)
                            return I18n.tr("Nothing waiting");

                        return I18n.trn("%1 notification", "%1 notifications", root.notifCount);
                    }
                    color: (Notifs.dnd || Notifs.quietNow) ? Theme.accent : Theme.subtext
                    font.family: Theme.fontFamily
                    font.pixelSize: Theme.fs(11)
                }

                NotifTextButton {
                    anchors.right: parent.right
                    anchors.verticalCenter: parent.verticalCenter
                    visible: Notifs.rows.length > 0 && Prefs.notifGrouping
                    label: I18n.tr("Expand all")
                    labelColor: Theme.subtext
                    onClicked: Notifs.expandAll()
                }

            }

            // nothing to show: say so properly rather than with a bare line
            Item {
                width: parent.width
                height: root.notifCount === 0 ? 132 : 0
                opacity: root.notifCount === 0 ? 1 : 0
                visible: opacity > 0.01
                clip: true

                Column {
                    anchors.centerIn: parent
                    spacing: 10

                    NotifIcon {
                        anchors.horizontalCenter: parent.horizontalCenter
                        size: 34
                        path: Notifs.icons.done_all
                        color: Theme.subtextDim
                    }

                    Text {
                        anchors.horizontalCenter: parent.horizontalCenter
                        text: I18n.tr("You're all caught up")
                        color: Theme.subtext
                        font.family: Theme.fontFamily
                        font.weight: Font.DemiBold
                        font.pixelSize: Theme.fs(12)
                    }

                    Text {
                        anchors.horizontalCenter: parent.horizontalCenter
                        text: Notifs.dnd ? I18n.tr("Notifications are being held back") : I18n.tr("New notifications will show up here")
                        color: Theme.subtextDim
                        font.family: Theme.fontFamily
                        font.pixelSize: Theme.fs(10)
                    }

                }

                Behavior on opacity {
                    NumberAnimation {
                        duration: Theme.barMs(200)
                    }

                }

            }

            // a Flickable rather than a ListView: one row here is a whole app
            // group, and an expanded one is far taller than the viewport.
            // ListView virtualises from delegate bounds, so a single huge
            // delegate whose height is also animated leaves its bookkeeping
            // stale and shows empty space. there are only ever a few rows.
            Flickable {
                id: shadeList

                // the gutter the scrollbar lives in, taken out of the panel's
                // right margin so the cards keep even margins either side
                readonly property int gutter: 10
                readonly property bool scrollable: shadeList.contentHeight > shadeList.height + 1
                readonly property real maxScroll: Math.max(0, shadeList.contentHeight - shadeList.height)

                function clampScroll() {
                    const v = Math.max(0, Math.min(shadeList.maxScroll, shadeList.contentY));
                    // guarded, or the assignment re-enters through onContentYChanged
                    if (Math.abs(v - shadeList.contentY) > 0.01)
                        shadeList.contentY = v;

                }

                // interactive is false, so nothing rebounds contentY on its own
                onContentHeightChanged: Qt.callLater(shadeList.clampScroll)
                onHeightChanged: Qt.callLater(shadeList.clampScroll)
                onContentYChanged: Qt.callLater(shadeList.clampScroll)

                width: parent.width + shadeList.gutter
                visible: root.notifCount > 0
                clip: true
                // the cards swipe sideways, so the list must not fight them
                interactive: false
                contentWidth: shadeList.width
                contentHeight: rowColumn.implicitHeight
                height: Math.max(0, Math.min(contentHeight, root.maxPanelHeight - root.chromeHeight))

                Column {
                    id: rowColumn

                    width: shadeList.width - shadeList.gutter
                    spacing: 6

                    // a row still fading in when the next one lands has its add cut short
                    // by this move, so the move finishes the fade or the row stays faint
                    move: Transition {
                        NumberAnimation {
                            properties: "x,y"
                            duration: Theme.barMs(300)
                            easing.type: Easing.Bezier
                            easing.bezierCurve: Theme.easeEmphasizedDecel
                        }

                        NumberAnimation {
                            property: "opacity"
                            to: 1
                            duration: Theme.barMs(200)
                        }

                    }

                    add: Transition {
                        NumberAnimation {
                            property: "opacity"
                            from: 0
                            to: 1
                            duration: Theme.barMs(240)
                            easing.type: Easing.OutCubic
                        }

                    }

                    Repeater {
                        model: shadeModel

                        Item {
                            id: shadeRow

                            required property var modelData

                            readonly property bool isHeader: shadeRow.modelData && shadeRow.modelData.kind === "header"

                            width: rowColumn.width
                            implicitHeight: shadeRow.isHeader ? 24 : groupItem.implicitHeight
                            height: shadeRow.implicitHeight

                            Text {
                                anchors.left: parent.left
                                anchors.leftMargin: 4
                                anchors.bottom: parent.bottom
                                anchors.bottomMargin: 4
                                visible: shadeRow.isHeader
                                text: shadeRow.isHeader ? shadeRow.modelData.label : ""
                                color: Theme.subtextDim
                                font.family: Theme.fontFamily
                                font.weight: Font.DemiBold
                                font.pixelSize: Theme.fs(10)
                                font.letterSpacing: 0.6
                            }

                            NotifGroup {
                                id: groupItem

                                width: parent.width
                                visible: !shadeRow.isHeader
                                row: shadeRow.isHeader ? null : shadeRow.modelData
                            }

                        }

                    }

                }

                MouseArea {
                    anchors.fill: parent
                    acceptedButtons: Qt.NoButton
                    onWheel: (wheel) => {
                        shadeList.contentY = Math.max(0, Math.min(shadeList.maxScroll, shadeList.contentY - (wheel.angleDelta.y / 120) * 90));
                        wheel.accepted = true;
                    }
                }

                ScrollBar.vertical: ScrollBar {
                    policy: shadeList.scrollable ? ScrollBar.AsNeeded : ScrollBar.AlwaysOff

                    contentItem: Rectangle {
                        implicitWidth: 3
                        radius: width / 2
                        color: Theme.accent
                        opacity: shadeList.scrollable ? 1 : 0

                        Behavior on opacity {
                            NumberAnimation {
                                duration: Theme.barMs(180)
                            }

                        }

                    }

                    background: Item {
                    }

                }

            }

            // m3 puts the shade's verbs on pills along the bottom
            Item {
                id: footerRow

                width: parent.width
                height: 34

                Rectangle {
                    anchors.top: parent.top
                    width: parent.width
                    height: 1
                    color: Theme.outline
                    opacity: 0.5
                }

                NotifTextButton {
                    anchors.right: parent.right
                    anchors.bottom: parent.bottom
                    label: I18n.tr("Clear all")
                    enabled: root.notifCount > 0
                    onClicked: Notifs.clearAll()
                }

            }

        }
    ]

    ScriptModel {
        id: shadeModel

        values: Notifs.rows
    }

    ScriptModel {
        id: stackModel

        values: root.restPopups
    }

}
