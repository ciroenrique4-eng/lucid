import QtQuick
import QtQuick.Shapes
import Quickshell
import Quickshell.Io
import Quickshell.Services.Pipewire
import Quickshell.Wayland
import qs
import "../luciddocks"

PanelWindow {
    id: osdWindow

    property bool ready: false
    property bool cardVisible: false
    property string oscType: ""
    property real levelValue: 0
    property bool levelMuted: false
    property bool toggleState: false
    readonly property var sink: Pipewire.defaultAudioSink
    readonly property var source: Pipewire.defaultAudioSource
    readonly property int volumePercent: (sink && sink.audio) ? Math.round(sink.audio.volume * 100) : 0
    readonly property bool volMuted: (sink && sink.audio) ? sink.audio.muted : false
    readonly property bool micMuted: (source && source.audio) ? source.audio.muted : false
    property string backlightDevice: ""
    property int maxBrightness: 0
    property int pendingBrightness: -1
    readonly property int brightnessPercent: osdWindow.maxBrightness > 0 ? Math.round((parseInt(brightnessFile.text()) / osdWindow.maxBrightness) * 100) : 0
    // the keyboard backlight is an led, not a backlight device, and steps
    // through whole levels rather than a percentage
    property string kbdDevice: ""
    property int kbdMax: 0
    readonly property string kbdWatcher: Qt.resolvedUrl("kbd-backlight-watch.py").toString().replace("file://", "")
    // the level the card last reported; the first reading is the current
    // state, not news, so it seeds this instead of showing anything
    property int kbdLast: -1
    property int kbdLevel: 0
    property bool kbdFileWritten: false
    // a handful of steps reads better as blocks than as a bar; a keyboard with
    // a fine-grained level keeps the track
    readonly property bool kbdSegmented: osdWindow.kbdMax > 0 && osdWindow.kbdMax <= 6
    property bool capsLock: false
    property bool numLock: false
    property bool kbInitialized: false
    // the level the last brightness click was for: a step clicks, a jump (the
    // idle dim, its undoing) does not
    property int brightnessHeard: -1

    // m3 shape, spacing and slider metrics, shared with lucidbar/System.qml.
    // the island keeps them; a notch is a size the user sets, so its own scale
    // with the card rather than filling it edge to edge
    readonly property int cardPadX: osdWindow.notch ? Math.max(10, Math.round(osdWindow.notchHeight * 0.21)) : 16
    readonly property int badgeSize: osdWindow.notch ? Math.min(44, osdWindow.notchHeight - osdWindow.notchPad * 2) : 44
    readonly property int cardGap: osdWindow.notch ? Math.max(8, Math.round(osdWindow.notchHeight * 0.18)) : 14
    readonly property int trackWidth: 200
    readonly property int readoutWidth: 50
    readonly property int glyphSize: osdWindow.notch ? Math.round(osdWindow.badgeSize / 2) : 22
    // how much card is left around the badge
    readonly property int notchPad: Math.max(6, Math.round(osdWindow.notchHeight * 0.14))
    readonly property int trackHeight: Math.max(8, Math.min(16, Math.round(osdWindow.badgeSize * 0.36)))
    // "VOLUME" over the track only while there is room for both
    readonly property bool showOverline: !osdWindow.notch || osdWindow.notchHeight >= 64
    readonly property int columnGap: osdWindow.notch ? Math.max(3, Math.round(osdWindow.notchHeight * 0.09)) : 7

    // notch style: the card sits flush on the bottom edge and slides out of it
    readonly property bool notch: Prefs.osdNotch
    // the notch card's size from settings; its width goes to the level track,
    // and a toggle card still grows to fit its label
    readonly property int notchWidth: Prefs.osdNotchWidth
    readonly property int notchHeight: Prefs.osdNotchHeight
    readonly property int levelChrome: osdWindow.cardPadX * 2 + osdWindow.badgeSize + osdWindow.cardGap * 2 + osdWindow.readoutWidth
    readonly property int levelTrackWidth: osdWindow.notch ? Math.max(80, osdWindow.notchWidth - osdWindow.levelChrome) : osdWindow.trackWidth

    // level 0 is the slash-free glyph: the badge draws its own slash
    readonly property var volumeIconLevels: [{
        "max": 0,
        "path": "M7 9v6h4l5 5V4l-5 5H7z"
    }, {
        "max": 49,
        "path": "M18.5 12c0-1.77-1.02-3.29-2.5-4.03v8.05c1.48-.73 2.5-2.25 2.5-4.02zM5 9v6h4l5 5V4L9 9H5z"
    }, {
        "max": 100,
        "path": "M3 9v6h4l5 5V4L7 9H3zm13.5 3c0-1.77-1.02-3.29-2.5-4.03v8.05c1.48-.73 2.5-2.25 2.5-4.02zM14 3.23v2.06c2.89.86 5 3.54 5 6.71s-2.11 5.85-5 6.71v2.06c4.01-.91 7-4.49 7-8.77s-2.99-7.86-7-8.77z"
    }]
    readonly property var brightnessIconLevels: [{
        "max": 33,
        "path": "M20 15.31L23.31 12 20 8.69V4h-4.69L12 .69 8.69 4H4v4.69L.69 12 4 15.31V20h4.69L12 23.31 15.31 20H20v-4.69zM12 18c-3.31 0-6-2.69-6-6s2.69-6 6-6 6 2.69 6 6-2.69 6-6 6z"
    }, {
        "max": 66,
        "path": "M20 15.31L23.31 12 20 8.69V4h-4.69L12 .69 8.69 4H4v4.69L.69 12 4 15.31V20h4.69L12 23.31 15.31 20H20v-4.69zM12 18V6c3.31 0 6 2.69 6 6s-2.69 6-6 6z"
    }, {
        "max": 100,
        "path": "M20 8.69V4h-4.69L12 .69 8.69 4H4v4.69L.69 12 4 15.31V20h4.69L12 23.31 15.31 20H20v-4.69L23.31 12 20 8.69zM12 18c-3.31 0-6-2.69-6-6s2.69-6 6-6 6 2.69 6 6-2.69 6-6 6zm0-10c-2.21 0-4 1.79-4 4s1.79 4 4 4 4-1.79 4-4-1.79-4-4-4z"
    }]
    readonly property string kbdIconPath: "M20 5H4c-1.1 0-1.99.9-1.99 2L2 17c0 1.1.9 2 2 2h16c1.1 0 2-.9 2-2V7c0-1.1-.9-2-2-2zm-9 3h2v2h-2V8zm0 3h2v2h-2v-2zM8 8h2v2H8V8zm0 3h2v2H8v-2zm-1 2H5v-2h2v2zm0-3H5V8h2v2zm9 7H8v-2h8v2zm0-4h-2v-2h2v2zm0-3h-2V8h2v2zm3 3h-2v-2h2v2zm0-3h-2V8h2v2z"
    readonly property var kbdIconLevels: [{
        "max": 100,
        "path": osdWindow.kbdIconPath
    }]
    readonly property string micIconPath: "M12 14c1.66 0 2.99-1.34 2.99-3L15 5c0-1.66-1.34-3-3-3S9 3.34 9 5v6c0 1.66 1.34 3 3 3zm5.3-3c0 3-2.54 5.1-5.3 5.1S6.7 14 6.7 11H5c0 3.41 2.72 6.23 6 6.72V21h2v-3.28c3.28-.48 6-3.3 6-6.72h-1.7z"
    readonly property string capsLockIconPath: "M12 8.41 16.59 13 18 11.59l-6-6-6 6L7.41 13 12 8.41ZM6 18h12v-2H6Z"
    readonly property string numLockIconPath: "M4.9 6.6a1.7 1.7 0 1 0 3.4 0a1.7 1.7 0 1 0 -3.4 0M10.3 6.6a1.7 1.7 0 1 0 3.4 0a1.7 1.7 0 1 0 -3.4 0M15.7 6.6a1.7 1.7 0 1 0 3.4 0a1.7 1.7 0 1 0 -3.4 0M4.9 12a1.7 1.7 0 1 0 3.4 0a1.7 1.7 0 1 0 -3.4 0M10.3 12a1.7 1.7 0 1 0 3.4 0a1.7 1.7 0 1 0 -3.4 0M15.7 12a1.7 1.7 0 1 0 3.4 0a1.7 1.7 0 1 0 -3.4 0M4.9 17.4a1.7 1.7 0 1 0 3.4 0a1.7 1.7 0 1 0 -3.4 0M10.3 17.4a1.7 1.7 0 1 0 3.4 0a1.7 1.7 0 1 0 -3.4 0M15.7 17.4a1.7 1.7 0 1 0 3.4 0a1.7 1.7 0 1 0 -3.4 0"
    readonly property bool isLevelType: osdWindow.oscType === "volume" || osdWindow.oscType === "brightness" || (osdWindow.oscType === "kbdbacklight" && !osdWindow.kbdSegmented)
    readonly property bool isSegmentType: osdWindow.oscType === "kbdbacklight" && osdWindow.kbdSegmented
    // both wear the badge + overline + readout anatomy; only the middle differs
    readonly property bool hasLevelRow: osdWindow.isLevelType || osdWindow.isSegmentType
    readonly property bool badgeActive: osdWindow.toggleState
    readonly property bool showMuteSlash: (osdWindow.oscType === "mic" && !osdWindow.toggleState) || (osdWindow.oscType === "volume" && osdWindow.levelMuted) || (osdWindow.oscType === "kbdbacklight" && osdWindow.kbdLevel === 0)
    readonly property string toggleIconPath: {
        switch (osdWindow.oscType) {
        case "mic":
            return osdWindow.micIconPath;
        case "capslock":
            return osdWindow.capsLockIconPath;
        case "numlock":
            return osdWindow.numLockIconPath;
        case "kbdbacklight":
            return osdWindow.kbdIconPath;
        default:
            return "";
        }
    }
    readonly property string currentLabel: {
        switch (osdWindow.oscType) {
        case "volume":
            return "Volume";
        case "brightness":
            return "Brightness";
        case "mic":
            return "Microphone";
        case "capslock":
            return "Caps Lock";
        case "numlock":
            return "Num Lock";
        case "kbdbacklight":
            return "Keyboard";
        default:
            return "";
        }
    }
    // the lock keys show the letters the next keystroke makes rather than a word
    readonly property string toggleOnText: {
        switch (osdWindow.oscType) {
        case "mic":
            return "Unmuted";
        case "capslock":
            return "ABC";
        case "numlock":
            return "123";
        default:
            return "";
        }
    }
    readonly property string toggleOffText: {
        switch (osdWindow.oscType) {
        case "mic":
            return "Muted";
        case "capslock":
            return "abc";
        case "numlock":
            return "Arrows";
        default:
            return "";
        }
    }
    readonly property color toggleOffColor: osdWindow.oscType === "mic" ? Theme.error : Theme.subtextDim

    // shows the card while its size is dragged in settings, without the icon pulse
    function previewSize() {
        if (!osdWindow.ready || !osdWindow.notch)
            return ;

        if (!osdWindow.isLevelType) {
            osdWindow.oscType = "volume";
            osdWindow.levelValue = osdWindow.volumePercent;
            osdWindow.levelMuted = osdWindow.volMuted;
        }
        osdWindow.setCardVisible(true);
        hideTimer.restart();
    }

    // the params have to be set before the flag flips: a Behavior reads the
    // previous value of anything its animation binds to
    function setCardVisible(v) {
        cardFade.duration = v ? (osdWindow.notch ? Theme.durQuick : Theme.durEnter) : Theme.durExit;
        cardFade.easing.bezierCurve = v ? Theme.easeEmphasizedDecel : Theme.easeEmphasizedAccel;
        cardSlide.duration = v ? Theme.durEnter : Theme.durExit;
        cardSlide.easing.bezierCurve = v ? Theme.easeEmphasizedDecel : Theme.easeEmphasizedAccel;
        cardRise.duration = v ? Theme.durEnter : Theme.durExit;
        cardRise.easing.bezierCurve = v ? Theme.easeEmphasizedDecel : Theme.easeEmphasizedAccel;
        cardPop.duration = v ? Theme.durEnter : Theme.durExit;
        if (v) {
            cardPop.easing.type = Easing.OutBack;
            cardPop.easing.overshoot = Theme.emphasizedOvershoot;
        } else {
            cardPop.easing.bezierCurve = Theme.easeEmphasizedAccel;
            cardPop.easing.type = Easing.Bezier;
        }
        osdWindow.cardVisible = v;
    }

    function trigger() {
        osdWindow.setCardVisible(true);
        hideTimer.restart();
        if (osdWindow.isLevelType)
            levelPulseAnim.restart();
        else if (osdWindow.oscType === "mic")
            pulseAnim.restart();
        else
            nudgeAnim.restart();
    }

    // a click on every step, like the notches of a dial: each press, each
    // repeat of a held key, and a drag no more often than every 35 ms, so it
    // stays a ratchet rather than a buzz
    function volumeClick() {
        Sounds.play("volume");
    }

    function showVolume() {
        if (Prefs.controlCentreOpen)
            return ;

        osdWindow.oscType = "volume";
        osdWindow.levelValue = osdWindow.volumePercent;
        osdWindow.levelMuted = osdWindow.volMuted;
        osdWindow.trigger();
    }

    function showBrightness() {
        if (Prefs.controlCentreOpen)
            return ;

        osdWindow.oscType = "brightness";
        osdWindow.levelValue = osdWindow.brightnessPercent;
        osdWindow.levelMuted = false;
        osdWindow.trigger();
    }

    function showKbdBacklight() {
        osdWindow.oscType = "kbdbacklight";
        osdWindow.levelValue = osdWindow.kbdMax > 0 ? Math.round((osdWindow.kbdLevel / osdWindow.kbdMax) * 100) : 0;
        osdWindow.levelMuted = false;
        osdWindow.trigger();
    }

    // caps lock, num lock and the microphone are on/off news; with toasts chosen
    // for them ToastEvents shows it and the card stays down
    signal toggled(string kind, bool on)

    function showToggle(kind, on) {
        osdWindow.toggled(kind, on);
        if (Prefs.osdTogglesToast)
            return ;

        osdWindow.oscType = kind;
        osdWindow.toggleState = on;
        osdWindow.trigger();
    }

    function showMic() {
        osdWindow.showToggle("mic", !osdWindow.micMuted);
    }

    function showCaps(state) {
        osdWindow.showToggle("capslock", state);
    }

    function showNum(state) {
        osdWindow.showToggle("numlock", state);
    }

    // the cursor on the level track, frac 0..1 of its width
    function setLevelAt(frac) {
        const pct = Math.round(Math.max(0, Math.min(1, frac)) * 100);
        if (osdWindow.oscType === "volume") {
            if (!osdWindow.sink || !osdWindow.sink.audio)
                return ;

            osdWindow.levelValue = pct;
            osdWindow.levelMuted = false;
            osdWindow.sink.audio.muted = false;
            osdWindow.sink.audio.volume = pct / 100;
        } else if (osdWindow.oscType === "brightness") {
            // never drag the panel fully dark
            osdWindow.levelValue = Math.max(1, pct);
            osdWindow.pendingBrightness = Math.max(1, pct);
            if (!brightnessThrottle.running)
                brightnessThrottle.start();

        }
    }

    color: "transparent"
    // -1 sits on the screen edge, past a bar a fullscreen window covers
    exclusiveZone: Monitors.barCoveredOn(osdWindow.screen) ? -1 : 0
    WlrLayershell.layer: WlrLayer.Overlay
    WlrLayershell.keyboardFocus: WlrKeyboardFocus.None
    // remapped with the rest of the shell when displays change
    visible: Monitors.surfacesUp
    implicitWidth: osdWindow.notch ? Math.max(480, card.width + 2 * Prefs.dockNotchFlare + 8) : 480
    implicitHeight: osdWindow.notch ? osdWindow.notchHeight : 140
    margins.bottom: osdWindow.notch ? 0 : 96
    Component.onCompleted: {
        findDeviceProc.running = true;
        findKbdProc.running = true;
        // the layer-shell surface's first commit ignores margins.bottom when it
        // already equals its bound value at creation (quickshell/Aquamarine bug);
        // nudging it once, a tick after creation, forces a real reconfigure
        marginKickTimer.start();
    }
    onNotchWidthChanged: osdWindow.previewSize()
    onNotchHeightChanged: osdWindow.previewSize()
    onBacklightDeviceChanged: {
        if (backlightDevice !== "")
            readMaxProc.running = true;

    }
    onKbdDeviceChanged: {
        if (kbdDevice === "")
            return ;

        readKbdMaxProc.running = true;
        // the watcher's command is set here rather than bound: the process
        // takes its argument list as it starts, and a binding has not caught
        // up with the device in the same pass that set it - it would be
        // launched on the empty path it had before
        kbdWatchProc.command = ["python3", osdWindow.kbdWatcher, "/sys/class/leds/" + osdWindow.kbdDevice];
        kbdWatchProc.running = true;
    }
    // both sources land here: the watcher for the key, the file for a level
    // set by anything that writes it
    // `written` marks a level read after a write to the file. that is news
    // even when it matches the level the card last showed: the key may have
    // moved the led since without the card hearing of it, if the watcher is
    // not there to report it
    function reportKbdLevel(level, written) {
        if (level < 0 || (level === osdWindow.kbdLast && !written))
            return ;

        const seeding = osdWindow.kbdLast < 0;
        osdWindow.kbdLast = level;
        osdWindow.kbdLevel = level;
        if (seeding || !osdWindow.ready)
            return ;

        osdWindow.showKbdBacklight();
    }
    // while the track is dragged the card already shows the value; re-showing would
    // pulse the handle and snap the track back to a lagging reading
    onVolumePercentChanged: {
        if (!osdWindow.ready)
            return ;

        if (!osdWindow.volMuted)
            osdWindow.volumeClick();

        if (levelDrag.pressed)
            return ;

        osdWindow.showVolume();
    }
    onVolMutedChanged: {
        if (!osdWindow.ready || levelDrag.pressed)
            return ;

        osdWindow.showVolume();
    }
    onBrightnessPercentChanged: {
        const was = osdWindow.brightnessHeard;
        osdWindow.brightnessHeard = osdWindow.brightnessPercent;
        if (osdWindow.ready && was >= 0 && Math.abs(osdWindow.brightnessPercent - was) <= 10)
            Sounds.play("brightness");

        if (!osdWindow.ready || levelDrag.pressed)
            return ;

        osdWindow.showBrightness();
    }
    onMicMutedChanged: {
        if (!osdWindow.ready)
            return ;

        osdWindow.showMic();
    }
    BackgroundEffect.blurRegion: (Theme.blurAmount > 0 && card.visible) ? osdBlurRegion : null

    anchors {
        bottom: true
    }

    PwObjectTracker {
        objects: [osdWindow.sink, osdWindow.source]
    }

    Timer {
        interval: 800
        running: true
        onTriggered: osdWindow.ready = true
    }

    // see Component.onCompleted: bump margins.bottom off its bound value, then
    // restore the live binding a tick later
    Timer {
        id: marginKickTimer

        interval: 50
        onTriggered: {
            osdWindow.margins.bottom = osdWindow.margins.bottom + 1;
            marginRestoreTimer.start();
        }
    }

    Timer {
        id: marginRestoreTimer

        interval: 50
        onTriggered: osdWindow.margins.bottom = Qt.binding(function() {
            return osdWindow.notch ? 0 : 96;
        })
    }

    Timer {
        id: hideTimer

        interval: 1600
        onTriggered: {
            // the cursor holds the card open; leaving it restarts this timer
            if (levelDrag.holding)
                return ;

            osdWindow.setCardVisible(false);
        }
    }

    // spaced out so a drag does not spawn a brightnessctl per mouse move; the
    // last value still lands once the running one exits
    Timer {
        id: brightnessThrottle

        interval: 50
        onTriggered: {
            if (setBrightnessProc.running)
                brightnessThrottle.restart();
            else if (osdWindow.pendingBrightness >= 0)
                setBrightnessProc.running = true;
        }
    }

    Process {
        id: setBrightnessProc

        command: osdWindow.pendingBrightness >= 0 ? ["brightnessctl", "set", osdWindow.pendingBrightness + "%"] : []
    }

    Process {
        id: findDeviceProc

        command: ["bash", "-c", "ls /sys/class/backlight | head -1"]

        stdout: StdioCollector {
            onStreamFinished: osdWindow.backlightDevice = this.text.trim().replace(/[@/*=|]$/, "")
        }

    }

    Process {
        id: readMaxProc

        command: osdWindow.backlightDevice ? ["cat", "/sys/class/backlight/" + osdWindow.backlightDevice + "/max_brightness"] : []

        stdout: StdioCollector {
            onStreamFinished: osdWindow.maxBrightness = parseInt(this.text.trim())
        }

    }

    FileView {
        id: brightnessFile

        path: osdWindow.backlightDevice ? "/sys/class/backlight/" + osdWindow.backlightDevice + "/brightness" : ""
        watchChanges: true
        onFileChanged: reload()
    }

    // vendor-prefixed: dell::kbd_backlight, tpacpi::kbd_backlight, asus::kbd_backlight
    Process {
        id: findKbdProc

        command: ["bash", "-c", "ls /sys/class/leds 2>/dev/null | grep -m1 'kbd_backlight$' || true"]

        stdout: StdioCollector {
            onStreamFinished: osdWindow.kbdDevice = this.text.trim()
        }

    }

    Process {
        id: readKbdMaxProc

        command: osdWindow.kbdDevice ? ["cat", "/sys/class/leds/" + osdWindow.kbdDevice + "/max_brightness"] : []

        stdout: StdioCollector {
            onStreamFinished: osdWindow.kbdMax = parseInt(this.text.trim()) || 0
        }

    }

    FileView {
        id: kbdFile

        path: osdWindow.kbdDevice ? "/sys/class/leds/" + osdWindow.kbdDevice + "/brightness" : ""
        watchChanges: true
        // the driver sets the led out of line with the write, so the file
        // still reads the old level right after the change lands; the reread
        // waits for it to settle
        onFileChanged: {
            osdWindow.kbdFileWritten = true;
            kbdSettleTimer.restart();
        }
        onLoaded: {
            osdWindow.reportKbdLevel(parseInt(text()) || 0, osdWindow.kbdFileWritten);
            osdWindow.kbdFileWritten = false;
        }
    }

    Timer {
        id: kbdSettleTimer

        interval: 200
        onTriggered: kbdFile.reload()
    }

    // the key is handled in firmware: the kernel moves the led and raises a
    // sysfs notification without anything writing the file, so watching the
    // file cannot see it. the helper sits on that notification and reports
    Process {
        id: kbdWatchProc

        stdout: SplitParser {
            onRead: (line) => {
                const level = parseInt(line.trim());
                if (!isNaN(level))
                    osdWindow.reportKbdLevel(level, false);

            }
        }

    }

    Process {
        id: kbStateProc

        command: ["hyprctl", "devices", "-j"]

        stdout: StdioCollector {
            onStreamFinished: {
                try {
                    var data = JSON.parse(this.text);
                    var kbs = data.keyboards || [];
                    var main = null;
                    for (var i = 0; i < kbs.length; i++) {
                        if (kbs[i].main) {
                            main = kbs[i];
                            break;
                        }
                    }
                    if (!main && kbs.length > 0)
                        main = kbs[0];

                    if (!main)
                        return ;

                    var newCaps = !!main.capsLock;
                    var newNum = !!main.numLock;
                    if (osdWindow.kbInitialized && osdWindow.ready) {
                        if (newCaps !== osdWindow.capsLock) {
                            osdWindow.capsLock = newCaps;
                            osdWindow.showCaps(newCaps);
                        }
                        if (newNum !== osdWindow.numLock) {
                            osdWindow.numLock = newNum;
                            osdWindow.showNum(newNum);
                        }
                    } else {
                        osdWindow.capsLock = newCaps;
                        osdWindow.numLock = newNum;
                    }
                    osdWindow.kbInitialized = true;
                } catch (e) {
                    console.warn("hyprctl devices parse failed:", e);
                }
            }
        }

    }

    Timer {
        interval: 400
        running: true
        repeat: true
        triggeredOnStart: true
        onTriggered: kbStateProc.running = true
    }

    Region {
        id: osdBlurRegion

        readonly property real paintedX: card.x + card.width * (1 - card.scale) / 2
        readonly property real paintedY: card.y + card.drop + card.height * (1 - card.scale) / 2
        readonly property real paintedWidth: card.width * card.scale
        readonly property real paintedHeight: card.height * card.scale

        x: Math.ceil(osdBlurRegion.paintedX - 0.002)
        y: Math.ceil(osdBlurRegion.paintedY - 0.002)
        width: Math.max(0, Math.floor(osdBlurRegion.paintedX + osdBlurRegion.paintedWidth + 0.002) - Math.ceil(osdBlurRegion.paintedX - 0.002))
        height: Math.max(0, Math.min(osdWindow.height, Math.floor(osdBlurRegion.paintedY + osdBlurRegion.paintedHeight + 0.002)) - Math.ceil(osdBlurRegion.paintedY - 0.002))
        radius: Math.round(card.radius * card.scale)
        bottomLeftRadius: osdWindow.notch ? 0 : osdBlurRegion.radius
        bottomRightRadius: osdWindow.notch ? 0 : osdBlurRegion.radius
    }

    Rectangle {
        id: card

        readonly property int levelWidth: osdWindow.levelChrome + osdWindow.levelTrackWidth
        readonly property int toggleWidth: osdWindow.cardPadX * 2 + osdWindow.badgeSize + osdWindow.cardGap + Math.ceil(Math.max(labelMetrics.advanceWidth, stateFlip.width))
        // how far the notch card is pushed below the screen edge
        property real drop: (osdWindow.notch && !osdWindow.cardVisible) ? card.height : 0

        anchors.horizontalCenter: parent.horizontalCenter
        // one vertical anchor for both styles, offset to the bottom edge for a
        // notch: swapping between bottom and verticalCenter at runtime makes the
        // anchor code write height itself, which drops the binding below and
        // leaves the card stuck at a size the other style chose
        anchors.verticalCenter: parent.verticalCenter
        anchors.verticalCenterOffset: osdWindow.notch ? Math.round((parent.height - card.height) / 2) : (osdWindow.cardVisible ? 0 : 16)
        height: osdWindow.notch ? osdWindow.notchHeight : 76
        width: osdWindow.hasLevelRow ? card.levelWidth : (osdWindow.notch ? Math.max(osdWindow.notchWidth, card.toggleWidth) : card.toggleWidth)
        radius: Math.min(Theme.shapeXlInc, card.height / 2)
        bottomLeftRadius: osdWindow.notch ? 0 : card.radius
        bottomRightRadius: osdWindow.notch ? 0 : card.radius
        color: Theme.bg
        opacity: osdWindow.cardVisible ? 1 : 0
        scale: (osdWindow.notch || osdWindow.cardVisible) ? 1 : 0.9
        visible: opacity > (osdWindow.notch ? 0.01 : 0.15)

        transform: Translate {
            y: card.drop
        }

        TextMetrics {
            id: labelMetrics

            text: osdWindow.currentLabel.toUpperCase()
            font.family: Theme.fontFamily
            font.bold: true
            font.pixelSize: Theme.fontLabelSm
            font.letterSpacing: 1.2
        }

        Rectangle {
            id: iconBadge

            anchors.left: parent.left
            anchors.leftMargin: osdWindow.cardPadX
            anchors.verticalCenter: parent.verticalCenter
            width: osdWindow.badgeSize
            height: osdWindow.badgeSize
            radius: Theme.pill(width)
            color: osdWindow.badgeActive ? Theme.accent : Theme.withBlur(Theme.bgHigh)

            MorphIcon {
                anchors.centerIn: parent
                visible: osdWindow.isLevelType
                levels: {
                    switch (osdWindow.oscType) {
                    case "brightness":
                        return osdWindow.brightnessIconLevels;
                    case "kbdbacklight":
                        return osdWindow.kbdIconLevels;
                    default:
                        return osdWindow.volumeIconLevels;
                    }
                }
                value: osdWindow.levelMuted ? 0 : osdWindow.levelValue
                tint: osdWindow.badgeActive ? "black" : (osdWindow.levelMuted ? Theme.error : Theme.text)
                iconSize: osdWindow.glyphSize
            }

            SvgIcon {
                id: toggleGlyph

                anchors.centerIn: parent
                visible: !osdWindow.isLevelType
                path: osdWindow.toggleIconPath
                tint: osdWindow.badgeActive ? "black" : Theme.text
                iconSize: osdWindow.glyphSize
            }

            // a cut in the badge colour, so the slash reads as a gap
            // through the glyph rather than a line laid over it
            Rectangle {
                visible: osdWindow.showMuteSlash
                anchors.centerIn: parent
                width: osdWindow.glyphSize * 1.3 + 4
                height: 5
                rotation: 45
                color: iconBadge.color
            }

            Rectangle {
                visible: osdWindow.showMuteSlash
                anchors.centerIn: parent
                width: osdWindow.glyphSize * 1.3
                height: 1.6
                radius: 1
                rotation: 45
                color: Theme.error
            }

            Behavior on color {
                ColorAnimation {
                    duration: Theme.durShort
                }

            }

        }

        // overline + slider, the m3 list-item anatomy System.qml's SliderRow uses
        Column {
            visible: osdWindow.hasLevelRow
            anchors.left: iconBadge.right
            anchors.leftMargin: osdWindow.cardGap
            anchors.verticalCenter: parent.verticalCenter
            spacing: osdWindow.columnGap

            Text {
                visible: osdWindow.showOverline
                text: osdWindow.currentLabel.toUpperCase()
                color: Theme.subtextDim
                font.family: Theme.fontFamily
                font.bold: true
                font.pixelSize: Theme.fontLabelSm
                font.letterSpacing: 1.2
            }

            SegmentTrack {
                id: segmentTrack

                visible: osdWindow.isSegmentType
                width: osdWindow.levelTrackWidth
                trackH: osdWindow.trackHeight
                steps: osdWindow.kbdMax
                level: osdWindow.kbdLevel
                animated: card.visible
            }

            LevelTrack {
                id: levelTrack

                visible: osdWindow.isLevelType
                width: osdWindow.levelTrackWidth
                trackH: osdWindow.trackHeight
                value: osdWindow.levelValue
                muted: osdWindow.levelMuted
                animated: card.visible && !levelDrag.pressed
            }

        }

        Item {
            id: readout

            visible: osdWindow.hasLevelRow
            width: osdWindow.readoutWidth
            height: 28
            anchors.right: parent.right
            anchors.rightMargin: osdWindow.cardPadX
            anchors.verticalCenter: parent.verticalCenter

            Text {
                id: pctSign

                anchors.right: parent.right
                anchors.baseline: pctNum.baseline
                text: osdWindow.isSegmentType ? "/" + osdWindow.kbdMax : "%"
                color: Theme.subtextDim
                font.family: Theme.fontFamily
                font.bold: true
                font.pixelSize: Theme.fontLabelSm
            }

            Text {
                id: pctNum

                anchors.right: pctSign.left
                anchors.rightMargin: 2
                anchors.verticalCenter: parent.verticalCenter
                text: osdWindow.isSegmentType ? osdWindow.kbdLevel : Math.round(levelTrack.value)
                color: (osdWindow.levelMuted || (osdWindow.isSegmentType && osdWindow.kbdLevel === 0)) ? Theme.subtextDim : Theme.text
                font.family: Theme.fontFamily
                font.bold: true
                font.pixelSize: osdWindow.showOverline ? Theme.fontTitleLg : Theme.fontTitle
            }

        }

        Column {
            visible: !osdWindow.hasLevelRow
            anchors.left: iconBadge.right
            anchors.leftMargin: osdWindow.cardGap
            anchors.verticalCenter: parent.verticalCenter
            spacing: osdWindow.notch ? Math.max(2, osdWindow.columnGap - 2) : 5

            Text {
                text: osdWindow.currentLabel.toUpperCase()
                color: Theme.subtextDim
                font.family: Theme.fontFamily
                font.bold: true
                font.pixelSize: Theme.fontLabelSm
                font.letterSpacing: 1.2
            }

            // off sits above on, so flipping rolls the column up one line
            Item {
                id: stateFlip

                readonly property int lineHeight: Math.ceil(offMetrics.height)

                width: Math.ceil(Math.max(onMetrics.advanceWidth, offMetrics.advanceWidth))
                height: stateFlip.lineHeight
                clip: true

                TextMetrics {
                    id: onMetrics

                    text: osdWindow.toggleOnText
                    font.family: Theme.fontFamily
                    font.bold: true
                    font.pixelSize: Theme.fontTitleSm
                }

                TextMetrics {
                    id: offMetrics

                    text: osdWindow.toggleOffText
                    font.family: Theme.fontFamily
                    font.bold: true
                    font.pixelSize: Theme.fontTitleSm
                }

                Column {
                    width: parent.width
                    y: osdWindow.toggleState ? -stateFlip.lineHeight : 0

                    Text {
                        width: parent.width
                        height: stateFlip.lineHeight
                        text: osdWindow.toggleOffText
                        color: osdWindow.toggleOffColor
                        font.family: Theme.fontFamily
                        font.bold: true
                        font.pixelSize: Theme.fontTitleSm
                    }

                    Text {
                        width: parent.width
                        height: stateFlip.lineHeight
                        text: osdWindow.toggleOnText
                        color: Theme.accent
                        font.family: Theme.fontFamily
                        font.bold: true
                        font.pixelSize: Theme.fontTitleSm
                    }

                    Behavior on y {
                        enabled: card.visible

                        NumberAnimation {
                            duration: Theme.durMedium
                            easing.type: Easing.Bezier
                            easing.bezierCurve: Theme.easeEmphasizedDecel
                        }

                    }

                }

            }

        }

        // drag or click the level track to set volume or brightness
        MouseArea {
            id: levelDrag

            // hovering only holds after real movement, so a card that slides in
            // under a resting cursor still hides on time
            property bool moved: false
            readonly property bool holding: levelDrag.pressed || (levelDrag.containsMouse && levelDrag.moved)

            function apply(mouse) {
                osdWindow.setLevelAt(levelDrag.mapToItem(levelTrack, mouse.x, mouse.y).x / Math.max(1, levelTrack.width));
            }

            anchors.fill: parent
            enabled: osdWindow.isLevelType
            hoverEnabled: true
            cursorShape: Qt.PointingHandCursor
            onPositionChanged: (mouse) => {
                levelDrag.moved = true;
                if (levelDrag.pressed)
                    levelDrag.apply(mouse);

            }
            onPressed: (mouse) => {
                levelDrag.moved = true;
                levelDrag.apply(mouse);
            }
            onExited: {
                levelDrag.moved = false;
                if (!levelDrag.pressed)
                    hideTimer.restart();

            }
            onReleased: {
                if (!levelDrag.containsMouse)
                    hideTimer.restart();

            }
        }

        NumberAnimation {
            id: nudgeAnim

            target: toggleGlyph
            property: "anchors.verticalCenterOffset"
            from: osdWindow.toggleState ? 7 : -7
            to: 0
            duration: Theme.durLong
            easing.type: Easing.OutBack
            easing.overshoot: 2.4
        }

        SequentialAnimation {
            id: pulseAnim

            NumberAnimation {
                target: iconBadge
                property: "scale"
                to: 1.12
                duration: Theme.durQuick
                easing.type: Easing.Bezier
                easing.bezierCurve: Theme.easeEmphasizedDecel
            }

            NumberAnimation {
                target: iconBadge
                property: "scale"
                to: 1
                duration: Theme.durShort
                easing.type: Easing.Bezier
                easing.bezierCurve: Theme.easeEmphasizedDecel
            }

        }

        // the handle answers the keypress, the way an m3 slider answers a press
        SequentialAnimation {
            id: levelPulseAnim

            NumberAnimation {
                target: levelTrack
                property: "handleStretch"
                to: 8
                duration: Theme.durQuick
                easing.type: Easing.Bezier
                easing.bezierCurve: Theme.easeEmphasizedDecel
            }

            NumberAnimation {
                target: levelTrack
                property: "handleStretch"
                to: 0
                duration: Theme.durMedium
                easing.type: Easing.OutBack
                easing.overshoot: Theme.emphasizedOvershoot
            }

        }

        Behavior on width {
            enabled: card.visible

            NumberAnimation {
                duration: Theme.durMedium
                easing.type: Easing.Bezier
                easing.bezierCurve: Theme.easeEmphasizedDecel
            }

        }

        Behavior on opacity {
            NumberAnimation {
                id: cardFade

                duration: Theme.durEnter
                easing.type: Easing.Bezier
                easing.bezierCurve: Theme.easeEmphasizedDecel
            }

        }

        Behavior on scale {
            NumberAnimation {
                id: cardPop

                duration: Theme.durEnter
                easing.type: Easing.OutBack
                easing.overshoot: Theme.emphasizedOvershoot
            }

        }

        Behavior on anchors.verticalCenterOffset {
            NumberAnimation {
                id: cardRise

                duration: Theme.durEnter
                easing.type: Easing.Bezier
                easing.bezierCurve: Theme.easeEmphasizedDecel
            }

        }

        Behavior on drop {
            enabled: osdWindow.notch

            NumberAnimation {
                id: cardSlide

                duration: Theme.durEnter
                easing.type: Easing.Bezier
                easing.bezierCurve: Theme.easeEmphasizedDecel
            }

        }

    }

    // concave corners joining the notch card to the screen edge, as the dock does
    Repeater {
        model: osdWindow.notch ? 2 : 0

        DockFlare {
            required property int index

            readonly property bool isRight: index === 1

            size: Prefs.dockNotchFlare
            mirrored: isRight
            x: isRight ? card.x + card.width : card.x - width
            y: card.y + card.height - height + card.drop
            opacity: card.opacity
            visible: card.visible
        }

    }

    // only a showing level card takes the cursor; everything else passes through
    mask: Region {
        x: card.x
        y: card.y + card.drop
        width: (osdWindow.isLevelType && osdWindow.cardVisible) ? card.width : 0
        height: (osdWindow.isLevelType && osdWindow.cardVisible) ? card.height : 0
    }

    component SvgIcon: Item {
        id: iconRoot

        property string path: ""
        property color tint: Theme.text
        property int iconSize: 16

        width: iconSize
        height: iconSize

        Shape {
            width: 24
            height: 24
            scale: iconRoot.iconSize / 24
            anchors.centerIn: parent
            preferredRendererType: Shape.CurveRenderer

            ShapePath {
                fillColor: iconRoot.tint
                strokeWidth: 0

                PathSvg {
                    path: iconRoot.path
                }

            }

        }

    }

    // the glyph crossfades between levels instead of snapping
    component MorphIcon: Item {
        id: morphIcon

        property var levels: []
        property real value: 0
        property color tint: Theme.text
        property int iconSize: 16
        readonly property int activeIndex: {
            for (let i = 0; i < morphIcon.levels.length; i++) {
                if (morphIcon.value <= morphIcon.levels[i].max)
                    return i;

            }
            return morphIcon.levels.length - 1;
        }

        width: iconSize
        height: iconSize

        Repeater {
            model: morphIcon.levels

            Shape {
                id: levelShape

                required property int index
                required property var modelData

                width: 24
                height: 24
                scale: morphIcon.iconSize / 24
                anchors.centerIn: parent
                preferredRendererType: Shape.CurveRenderer
                opacity: morphIcon.activeIndex === levelShape.index ? 1 : 0

                Behavior on opacity {
                    NumberAnimation {
                        duration: Theme.durShort
                        easing.type: Easing.OutCubic
                    }

                }

                ShapePath {
                    fillColor: morphIcon.tint
                    strokeWidth: 0

                    PathSvg {
                        path: levelShape.modelData.path
                    }

                }

            }

        }

    }

    // m3 slider anatomy: 16dp tracks notched 6dp either side of a 4dp handle,
    // squared off where they meet it, stop indicator on the inactive end
    // whole levels, so blocks rather than a bar. they share the track's width
    // the way the level does, but only the steps that are on have its full
    // body and the rest are a thin rail: two blocks of the same thickness
    // side by side read as one bar cut in half, whatever the gap. the sizes
    // come from the track, which the notch already scales with its own size
    component SegmentTrack: Item {
        id: st

        property int level: 0
        property int steps: 1
        property int trackH: 16
        property bool animated: false
        readonly property int count: Math.max(1, st.steps)
        readonly property int gap: Math.max(10, Math.round(st.trackH * 1.2))
        readonly property real cellWidth: Math.max(4, (st.width - st.gap * (st.count - 1)) / st.count)
        readonly property int railHeight: Math.max(4, Math.round(st.trackH * 0.38))

        height: st.trackH + 12

        Row {
            anchors.verticalCenter: parent.verticalCenter
            anchors.horizontalCenter: parent.horizontalCenter
            spacing: st.gap

            Repeater {
                model: st.count

                Item {
                    id: cell

                    required property int index
                    readonly property bool lit: cell.index < st.level

                    width: st.cellWidth
                    height: st.trackH

                    Rectangle {
                        anchors.centerIn: parent
                        width: parent.width
                        height: cell.lit ? st.trackH : st.railHeight
                        radius: Theme.pill(height)
                        color: cell.lit ? Theme.accent : Theme.withBlur(Theme.bgHigh)

                        Behavior on height {
                            enabled: st.animated

                            NumberAnimation {
                                duration: Theme.durShort
                                easing.type: Easing.Bezier
                                easing.bezierCurve: Theme.easeEmphasizedDecel
                            }

                        }

                        Behavior on color {
                            enabled: st.animated

                            ColorAnimation {
                                duration: Theme.durShort
                            }

                        }

                    }

                }

            }

        }

    }

    component LevelTrack: Item {
        id: lt

        property real value: 0
        property bool muted: false
        property bool animated: false
        property real handleStretch: 0
        readonly property int handleW: 4
        property int trackH: 16
        readonly property int notch: 6
        readonly property real pos: Math.max(0, Math.min(1, lt.value / 100))
        readonly property real handleX: lt.pos * Math.max(0, lt.width - lt.handleW)
        readonly property color liveColor: lt.muted ? Theme.outlineStrong : Theme.accent

        height: lt.trackH + 12

        Behavior on value {
            enabled: lt.animated

            NumberAnimation {
                duration: Theme.durShort
                easing.type: Easing.Bezier
                easing.bezierCurve: Theme.easeEmphasizedDecel
            }

        }

        Rectangle {
            id: activeTrack

            x: 0
            anchors.verticalCenter: parent.verticalCenter
            // below a couple of px the rounded stub reads as an artifact
            visible: activeTrack.width > 2
            width: Math.max(0, lt.handleX - lt.notch)
            height: lt.trackH
            radius: Theme.pill(lt.trackH)
            topRightRadius: 2
            bottomRightRadius: 2
            color: lt.liveColor

            Behavior on color {
                ColorAnimation {
                    duration: Theme.durShort
                }

            }

        }

        Rectangle {
            id: inactiveTrack

            x: Math.min(lt.width, lt.handleX + lt.handleW + lt.notch)
            anchors.verticalCenter: parent.verticalCenter
            width: Math.max(0, lt.width - inactiveTrack.x)
            height: lt.trackH
            radius: Theme.pill(lt.trackH)
            topLeftRadius: 2
            bottomLeftRadius: 2
            color: Theme.withBlur(Theme.bgHigh)

            Rectangle {
                visible: inactiveTrack.width > 16
                anchors.right: parent.right
                anchors.rightMargin: 6
                anchors.verticalCenter: parent.verticalCenter
                width: 4
                height: 4
                radius: 2
                color: Theme.outlineStrong
            }

        }

        Rectangle {
            x: lt.handleX
            anchors.verticalCenter: parent.verticalCenter
            width: lt.handleW
            height: lt.trackH + 12 + lt.handleStretch
            radius: lt.handleW / 2
            color: lt.liveColor

            Behavior on color {
                ColorAnimation {
                    duration: Theme.durShort
                }

            }

        }

    }

}
